set shell := [ "bash", "-c" ]

default:
	@just test

runtime_llh:

alloc_test:
	@just build alloc_test \
		generic/abort.ll \
		generic/alloc.ll \
		linux-x86_64/sys.ll \
		linux-x86_64/thread.ll \
		test/alloc_test.ll
	
	bin/alloc_test

blob_test:
	@just build blob_test \
		generic/abort.ll \
		generic/alloc.ll \
		generic/object.ll \
		generic/blob.ll \
		generic/ref.ll \
		linux-x86_64/sys.ll \
		linux-x86_64/thread.ll \
		test/blob_test.ll
	
	bin/blob_test

ref_test:
	@just build ref_test \
		generic/abort.ll \
		generic/alloc.ll \
		generic/object.ll \
		generic/ref.ll \
		linux-x86_64/sys.ll \
		linux-x86_64/thread.ll \
		test/ref_test.ll

	bin/ref_test

task_test:
	@just build task_test \
		generic/abort.ll \
		generic/alloc.ll \
		generic/task.ll \
		linux-x86_64/sys.ll \
		linux-x86_64/thread.ll \
		test/task_test.ll

	bin/task_test

build output +sources:
	for SOURCE in {{sources}}; do \
		( \
			find generic linux-x86_64 \
				\( \
					-name '*.llf' \
					-o -name '*.llt' \
				\) \
				-a -not -wholename ${SOURCE}f \
				-exec echo '; {}' ';' \
				-exec cat '{}' ';' \
				-exec echo ';'; \
			echo "; $SOURCE"; \
			cat $SOURCE; \
		) > tmp/`basename $SOURCE`; \
	done
	llvm-link-19 -S -o tmp/{{file_stem(output)}}.ll $(for SOURCE in {{sources}}; do echo tmp/`basename $SOURCE`; done)
	llc-19 -O=3 -filetype=obj -o tmp/{{file_stem(output)}}.o tmp/{{file_stem(output)}}.ll
	ld.lld-17 -o bin/{{output}} tmp/{{file_stem(output)}}.o

test:
	@just alloc_test
	@just blob_test
	@just ref_test
	@just task_test

clean:
	rm -f bin/* tmp/*
